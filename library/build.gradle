apply plugin: 'android-library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = "com.openxcplatform"
version = rootProject.ext.versionName

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.android.support:support-v4:19.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0-rc3'
    testCompile 'org.robolectric:shadows-httpclient:3.0-rc3'
}

def siteUrl = 'http://openxcplatform.com'
def gitUrl = 'https://github.com/openxc/openxc-android.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                name 'OpenXC Android Library'
                packaging 'aar'
                description 'OpenXC vehicle data library for Android applications'
                url siteUrl

                scm {
                    url 'https://github.com/openxc/openxc-android'
                    connection gitUrl
                    developerConnection gitUrl
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://opensource.org/licenses/BSD-2-Clause'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'peplin'
                        name 'Chris Peplin'
                        email 'chris.peplin@gmail.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayPassword

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "openxc-android"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = 'https://github.com/openxc/openxc-android/issues'
        licenses = ["BSD"]
        publish = true
    }
}
