apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

group = "com.openxcplatform"
version = rootProject.ext.versionName

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
        abortOnError false
    }
    useLibrary  'org.apache.http.legacy'
}

apply plugin: 'com.github.dcendents.android-maven'

dependencies {
    api 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.guava:guava:24.1-android'
    implementation 'com.google.protobuf:protobuf-java:3.5.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.android.support:support-v4:27.1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:2.17.0'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.robolectric:shadows-httpclient:3.8'
}

def siteUrl = 'http://openxcplatform.com'
def gitUrl = 'https://github.com/openxc/openxc-android.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                name 'OpenXC Android Library'
                packaging 'aar'
                description 'OpenXC vehicle data library for Android applications'
                url siteUrl

                scm {
                    url 'https://github.com/openxc/openxc-android'
                    connection gitUrl
                    developerConnection gitUrl
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://opensource.org/licenses/BSD-2-Clause'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'peplin'
                        name 'Chris Peplin'
                        email 'chris.peplin@gmail.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayPassword

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "openxc-android"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = 'https://github.com/openxc/openxc-android/issues'
        licenses = ["BSD"]
        publish = true
    }
}
