apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.bugsnag.android.gradle'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.openxcplatform.enabler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled true
    }

   signingConfigs {
        debug {
            storeFile rootProject.file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile rootProject.file('keys/release.keystore')
            storePassword rootProject.ext.keystorePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            buildConfigField "String", "BUGSNAG_TOKEN", "null"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-rules.txt'
            buildConfigField "String", "BUGSNAG_TOKEN", "\"" + rootProject.ext.bugsnagToken + "\""
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def newName = outputFileName
                newName.replace(".apk", "-${variant.versionName}.apk")
                outputFileName = new File(newName)
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
      exclude 'LICENSE.txt'
    }

    bugsnag {
        apiKey rootProject.ext.bugsnagToken
    }

    play {
        serviceAccountEmail = rootProject.ext.serviceAccountEmail
        pk12File = rootProject.file('keys/play-store.p12')
        track = 'beta'
        uploadImages = true
    }
}

dependencies {
    implementation project(":library")
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.bugsnag:bugsnag-android:4.3.2'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.mockito:mockito-core:2.17.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
}
