apply plugin: 'com.android.application'
apply plugin: 'bugsnag'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.openxcplatform.enabler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

   signingConfigs {
        debug {
            storeFile rootProject.file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile rootProject.file('keys/release.keystore')
            storePassword rootProject.ext.keystorePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            testCoverageEnabled = true
            buildConfigField "String", "BUGSNAG_TOKEN", "null"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.txt'
            buildConfigField "String", "BUGSNAG_TOKEN", "\"" + rootProject.ext.bugsnagToken + "\""
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
      exclude 'LICENSE.txt'
    }

    bugsnag {
        apiKey rootProject.ext.bugsnagToken
    }

    play {
        serviceAccountEmail = rootProject.ext.serviceAccountEmail
        pk12File = rootProject.file('keys/play-store.p12')
        track = 'beta'
        uploadImages = true
    }
}

dependencies {
    compile project(":library")

    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.bugsnag:bugsnag-android:3.2.2'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}
