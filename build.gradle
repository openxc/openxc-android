// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.2.5'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}


ext {
    compileSdkVersion = 27
    buildToolsVersion = "27.0.3"
    minSdkVersion = 18
    targetSdkVersion = 27

    versionMajor = 7
    versionMinor = 0
    versionPatch = 0

    versionCode = versionMajor * 1000000 + versionMinor * 1000 + versionPatch
    versionName = "${versionMajor}.${versionMinor}.${versionPatch}"

    def Properties properties = new Properties()
    if(file('local.properties').exists())
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

    keystorePassword = getValue(properties, "KEYSTORE_PASS")
    keyAlias = getValue(properties, "ALIAS_NAME")
    keyPassword = getValue(properties, "ALIAS_PASS")

    bugsnagToken = getValue(properties, "BUGSNAG_TOKEN")
    serviceAccountEmail = getValue(properties, "SERVICE_EMAIL")

    bintrayUser = getValue(properties, "BINTRAY_USER")
    bintrayPassword = getValue(properties, "BINTRAY_API_KEY")
}

def getValue(def props, def name){
    if(props && props[name])
        return props[name]
    else if(System.getenv(name))
        return System.getenv(name)
    else {
        logger.log(LogLevel.ERROR, name + " has not been provided, add it to your local.properties file")
        return "DEFAULT"
    }
}
